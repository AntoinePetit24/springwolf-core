plugins {
  id 'java'
  id 'com.moowork.node' version '1.2.0'

  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
}

version = rootProject.uiVersion
sourceCompatibility = '1.8'

node {
  version = '12.18.1'
  npmVersion = '6.14.5'
  download = true
}

npm_run_build {
  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  outputs.dir 'build'
}

jar {
  dependsOn 'npm_run_build'
  metaInf { from 'dist/springwolf-ui' into 'resources' }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}


def pomConfig = {
  licenses {
    license {
      name 'The Apache License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    }
  }

  developers {
    developer {
      id 'stavshamir'
      name 'Stav Shamir'
      email 'shamir.stav@gmail.com'
    }
  }

  scm {
    url 'https://github.com/stavshamir/springwolf'
  }
}

publishing {
  publications {
    mavenPublication(MavenPublication) {
      artifact jar

      artifact sourcesJar {
        classifier "sources"
      }

      groupId project.group
      artifactId 'springwolf-ui'
      version project.version
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'Automated JSON API documentation for Kafka Listeners built with Spring')
        root.appendNode('name', 'springwolf-ui')
        root.appendNode('url', 'https://github.com/stavshamir/springwolf')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = System.getProperty('bintray.user')
  key = System.getProperty('bintray.key')
  publications = ['mavenPublication']

  pkg {
    repo = 'springwolf'
    name = 'springwolf-ui'
    userOrg = 'stavshamir'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/stavshamir/springwolf.git'
    version {
      name = project.version
      desc = 'Frontend for springwolf - An automated JSON API documentation for Kafka Listeners built with Spring'
      released = new Date()
    }
  }
}
